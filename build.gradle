import org.gradle.internal.os.OperatingSystem

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

group 'io.github.gjkf'
version '0.6'

apply plugin: 'java'
apply plugin: 'application'

sourceSets {
    main {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

applicationDistribution.exclude("libraries/*")

def lwjglVersion = "3.1.1"

mainClassName = "com.gjkf.lunarLander.LunarLander"

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}

dependencies {
    // LWJGL dependencies START
    compile "org.lwjgl:lwjgl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-openal:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-stb:${lwjglVersion}"
    // LWJGL natives
    runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-openal:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
    // LWJGL dependencies END
    compile "org.joml:joml:1.9.0"
    compile "org.l33tlabs.twl:pngdecoder:1.0"

    compile "org.encog:encog-core:3.4.0-SNAPSHOT"

    /* Include all the jars */
    def allJars = fileTree(dir: 'libraries/', include: '*.jar')
    /* Include just the deobfuscated ones */
    def dJars = fileTree(dir: 'libraries/', include: '*-deobf.jar')
    /* Include just the source ones */
    def sJars = fileTree(dir: 'libraries/', include: '*-source.jar')
    /* Get the jars that are not deobf or source */
    def jarsToUse = allJars - dJars - sJars
    compile jarsToUse
}

jar{
    manifest{
        attributes(
                "Main-Class": mainClassName,
                "Implementation-Title": project.name,
                "Implementation-Version": project.version
        )
    }
}

// add source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'source'
}

// deobf jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

// make sure all of these happen when we run build
build.dependsOn sourceJar, deobfJar